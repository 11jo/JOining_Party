
// Cas d'un recrutement non standard (ctrl+Q par exemple)
IF
	!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	InParty(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
END

// Cas où le script serait quand même actif mais que le personnage ne répond à aucun cas répertorié :
// ni dans le groupe, ni familier, ni au sol, ni charmé
// ChangeEnemyAlly par exemple
IF
	!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
	!StateCheck(Myself,STATE_CHARMED)
	!Allegiance(Myself,FAMILIAR)
	!InParty(Myself)
	OR(2)
		!Allegiance(Myself,NEUTRAL)
		Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
THEN
	RESPONSE #100
		// reset block
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SetGlobal("JO_Myself_LeaveParty","LOCALS",0)
		SetGlobal("JO_%Death_var%_LeaveParty","GLOBAL",0)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOINI","LOCALS",0)
		// end reset block
END

// Base
 
// !InPartyAllowDead()
IF
	!Global("JO_Myself_AllowDead","LOCALS",0)
	!InPartyAllowDead(Myself)
	Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_AllowDead","LOCALS",0)
END

// InPartyAllowDead()
IF
	Global("JO_Myself_AllowDead","LOCALS",0)
	OR(2)
		InPartyAllowDead(Myself)
		!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_AllowDead","LOCALS",1)
END
 
// !InParty()
IF
	!Global("JO_Myself_InParty","LOCALS",0)
	!InParty(Myself)
	OR(2)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
		!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_InParty","LOCALS",0)
END
 
// InParty()
IF
	Global("JO_Myself_InParty","LOCALS",0)
	OR(2)
		InParty(Myself)
		!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
	OR(2)
		InParty(Myself)
		Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_InParty","LOCALS",1)
END
 
// !Is(!If)ValidForPartyDialog(ue)
IF
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0) // Temporary solution until DPLAYER2 is sorted out...
	!Global("JO_Myself_Valid","LOCALS",0)
	!IsValidForPartyDialogue(Myself)
	OR(5)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
		!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
		StateCheck(Myself,CD_STATE_NOTVALID)
		!IsActive(Myself)
		!CheckStat(Myself,0,CASTERHOLD)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_Valid","LOCALS",0)
END

// Is(If)ValidForPartyDialog(ue)
IF
	Global("JO_Myself_Valid","LOCALS",0)
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	IsActive(Myself)
	CheckStat(Myself,0,CASTERHOLD)
	OR(2)
		IsValidForPartyDialogue(Myself)
		!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		InParty(Myself)
		!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_Valid","LOCALS",1)
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

IF
	!Global("JO_TRAVELER_%Death_var%","GLOBAL",0)
	OR(2)
		!Global("JO_Myself_LeaveParty","LOCALS",0)
		!Global("JO_%Death_var%_LeaveParty","GLOBAL",0)
	!InParty(Myself)
THEN
	RESPONSE #100
		JoinParty()
		Continue()
END

IF
	OR(2)
		!Global("JO_Myself_LeaveParty","LOCALS",0)
		!Global("JO_%Death_var%_LeaveParty","GLOBAL",0)
	InParty(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",0)
		SetGlobal("JO_Myself_LeaveParty","LOCALS",0)
		SetGlobal("JO_%Death_var%_LeaveParty","GLOBAL",0)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOINI","LOCALS",0)
		SetGlobal("JO_Myself_AllowDead","LOCALS",0)
		SetGlobal("JO_Myself_InParty","LOCALS",0)
		SetGlobal("JO_Myself_Valid","LOCALS",0)
		LeaveParty()
END

IF
	!AreaCheck("BD0120")
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	OR(2)
		!Allegiance(Myself,FAMILIAR)
		InParty(Myself)
	OR(3)
		!Global("JO_TRAVELER_%Death_var%","GLOBAL",0)
		!Global("JO_Myself_LeaveParty","LOCALS",0)
		!Global("JO_%Death_var%_LeaveParty","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",0)
		SetGlobal("JO_Myself_LeaveParty","LOCALS",0)
		SetGlobal("JO_%Death_var%_LeaveParty","GLOBAL",0)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
END

IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_TRAVELER_%Death_var%","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",1)
END

///////////////////////////////////////////////////////////////////////////////

// Related to Join_ability.baf destroy item before 

IF
	!Allegiance(Myself,FAMILIAR)
	HasItem("JOMINHP1",Myself)
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
THEN
	RESPONSE #100
		EquipItemEx("JOMINHP1",1)
		DestroyItem("JOMINHP1")
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// First time from party to familiar

IF
	Global("JO_JOIN","LOCALS",1)
	Allegiance(Myself,PC)
	!InPartySlot(Myself,0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		LeaveParty()
		SmallWait(1)
		GiveItemCreate("JOMINHP1",Myself,0,0,0)
		SmallWait(1)
		EquipItemEx("JOMINHP1",0)
		SmallWait(1)
		ChangeEnemyAlly(Myself,FAMILIAR)
		AddFamiliar()
		FaceObject(Player1)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",1)
		SetGlobal("JO_JOIN_TALK","LOCALS",1) // fellow travelers
		SetGlobal("JO_JOIN","LOCALS",2)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobalTimer("JO_JOIN_JOIN","LOCALS",ONE_TURN)
		ChangeAIScript("JO_JOINI",GENERAL)
// JO_JOIN.BAF		ChangeAIScript("%JO_JOIN%",OVERRIDE) // Go to relevant %JO_JOIN%.baf
//		MakeGlobal()
		SetInterrupt(TRUE)
END

// fellow travelers (End first time from party to familiar)
 
IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_JOIN_TALK","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_TALK","LOCALS",0)
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",1)
		DisplayStringHead(Myself,~We are now fellow travelers.~)
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// Switch between familiar and party

IF
	Global("JO_JOINI","LOCALS",1)
	Allegiance(Myself,PC)
	!InPartySlot(Myself,0)
	Delay(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		LeaveParty()
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		SmallWait(1)
		GiveItemCreate("JOMINHP1",Myself,0,0,0)
		SmallWait(1)
		EquipItemEx("JOMINHP1",0)
		SmallWait(1)
		ChangeEnemyAlly(Myself,FAMILIAR)
		AddFamiliar()
		SmallWait(1)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",1)
		SetGlobal("JO_JOINI","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",2)
		SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS) // 20mn
// Add switch familiar related  script
//		ChangeAIScript("",RACE)
//		ChangeAIScript("",CLASS)
//		ChangeAIScript("",GENERAL)
		ChangeAIScript("JO_JOINI",GENERAL)
//		MakeGlobal()
// JO_JOIN.BAF		ChangeAIScript("%JO_JOIN%",OVERRIDE) // Go to relevant %JO_JOIN%.baf
		SetInterrupt(TRUE)
		Continue()
END
