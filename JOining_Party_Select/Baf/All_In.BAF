
// Base
 
// !InPartyAllowDead("%Death_var%") / !Allegiance("%Death_var%",FAMILIAR)
IF
	OR(2)
		!Global("JO_%Death_var%_AllowDead","GLOBAL",0)
		!Global("JO_Myself_AllowDead","LOCALS",0)
	!InPartyAllowDead(Myself)
	!Allegiance(Myself,FAMILIAR)
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_%Death_var%_AllowDead","GLOBAL",0)
		SetGlobal("JO_Myself_AllowDead","LOCALS",0)
END
 
// InPartyAllowDead("%Death_var%") / Allegiance("%Death_var%",FAMILIAR)
IF
	OR(2)
		Global("JO_%Death_var%_AllowDead","GLOBAL",0)
		Global("JO_Myself_AllowDead","LOCALS",0)
	OR(2)
		InPartyAllowDead(Myself)
		Allegiance(Myself,FAMILIAR)
THEN
	RESPONSE #100
		SetGlobal("JO_%Death_var%_AllowDead","GLOBAL",1)
		SetGlobal("JO_Myself_AllowDead","LOCALS",1)
END
 
// !InParty("%Death_var%") / !Allegiance("%Death_var%",FAMILIAR)
IF
	OR(2)
		!Global("JO_%Death_var%_InParty","GLOBAL",0)
		!Global("JO_Myself_InParty","LOCALS",0)
	!InParty(Myself)
	!Allegiance(Myself,FAMILIAR)
THEN
	RESPONSE #100
		SetGlobal("JO_%Death_var%_InParty","GLOBAL",0)
		SetGlobal("JO_Myself_InParty","LOCALS",0)
END
 
// InParty("%Death_var%") / Allegiance("%Death_var%",FAMILIAR)
IF
	OR(2)
		Global("JO_%Death_var%_InParty","GLOBAL",0)
		Global("JO_Myself_InParty","LOCALS",0)
	OR(2)
		InParty(Myself)
		Allegiance(Myself,FAMILIAR)
THEN
	RESPONSE #100
		SetGlobal("JO_%Death_var%_InParty","GLOBAL",1)
		SetGlobal("JO_Myself_InParty","LOCALS",1)
END
 
// !Is(!If)ValidForPartyDialog(ue)
IF
	OR(2)
		!Global("JO_%Death_var%_Valid","GLOBAL",0)
		!Global("JO_Myself_Valid","LOCALS",0)
	!IsValidForPartyDialogue(Myself)
	OR(4)
		!Allegiance(Myself,FAMILIAR)
		StateCheck(Myself,CD_STATE_NOTVALID)
		!Global("JO_JOIN_DEADLY_DEAD","LOCALS",0)
		!IsActive(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_%Death_var%_Valid","GLOBAL",0)
		SetGlobal("JO_Myself_Valid","LOCALS",0)
END
 
// Is(If)ValidForPartyDialog(ue)
IF
	Global("JO_JOIN_DEADLY_DEAD","LOCALS",0)
	OR(2)
		Global("JO_%Death_var%_Valid","GLOBAL",0)
		Global("JO_Myself_Valid","LOCALS",0)
	OR(2)
		IsValidForPartyDialogue(Myself)
		!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Allegiance(Myself,FAMILIAR)
		InParty(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_%Death_var%_Valid","GLOBAL",1)
		SetGlobal("JO_Myself_Valid","LOCALS",1)
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

IF
	Global("JO_TRAVELER_%Death_var%","GLOBAL",1)
	OR(2)
		Global("JO_Myself_LeaveParty","LOCALS",1)
		Global("JO_%Death_var%_LeaveParty","GLOBAL",1)
	!InParty(Myself)
THEN
	RESPONSE #100
		JoinParty()
END

IF
	OR(2)
		Global("JO_Myself_LeaveParty","LOCALS",1)
		Global("JO_%Death_var%_LeaveParty","GLOBAL",1)
	InParty(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",0)
		SetGlobal("JO_Myself_LeaveParty","LOCALS",0)
		SetGlobal("JO_%Death_var%_LeaveParty","GLOBAL",0)
		LeaveParty()
END

IF
	!AreaCheck("BD0120")
	!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",1)
	!Allegiance(Myself,FAMILIAR)
	Global("JO_TRAVELER_%Death_var%","GLOBAL",1)
	!Global("JO_JOIN","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",0)
END

IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_TRAVELER_%Death_var%","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",1)
END

/*
IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_TRAVELER_%Death_var%","GLOBAL",1)
	OR(2)
		Global("KickedOut","LOCALS",0)
		Global("IdKickedOut","LOCALS",0) 
THEN
	RESPONSE #100
		SetGlobal("KickedOut","LOCALS",1)
		SetGlobal("IdKickedOut","LOCALS",1) 
END

IF
	!AreaCheck("BD0120")
	!Allegiance(Myself,FAMILIAR)
	!InParty(Myself)
	!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",1)
	Global("JO_TRAVELER_%Death_var%","GLOBAL",0)
	OR(2)
		Global("KickedOut","LOCALS",1)
		Global("IdKickedOut","LOCALS",1) 
THEN
	RESPONSE #100
		SetGlobal("KickedOut","LOCALS",0)
		SetGlobal("IdKickedOut","LOCALS",0) 
END
*/

///////////////////////////////////////////////////////////////////////////////

// Related to Join_ability.baf destroy item before 

IF
	!Allegiance(Myself,FAMILIAR)
	HasItem("JOMINHP1",Myself)
THEN
	RESPONSE #100
		DestroyItem("JOMINHP1")
END

///////////////////////////////////////////////////////////////////////////////

// Related to Jo_JOINI.baf set statut when Dead but not really

IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_JOIN_DEADLY_DEAD","LOCALS",0)
	OR(2)
		Global("JO_JOIN_SLEEPING_DEAD","LOCALS",1)
		Global("JO_JOIN_SLEEPING_DEADLY","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_DEADLY_DEAD","LOCALS",1)
		SetGlobal("JO_%Death_var%_Valid","GLOBAL",0)
		SetGlobal("JO_Myself_Valid","LOCALS",0)
END

// Related to Jo_JOINI.baf set statut when No more Dead 

IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_JOIN_DEADLY_DEAD","LOCALS",1)
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Global("JO_JOIN_SLEEPING_DEADLY","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_DEADLY_DEAD","LOCALS",0)
		SetGlobal("JO_%Death_var%_Valid","GLOBAL",1)
		SetGlobal("JO_Myself_Valid","LOCALS",1)
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// First time from party to familiar

IF
	Global("JO_JOIN","LOCALS",1)
	Allegiance(Myself,PC)
	!InPartySlot(Myself,0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		LeaveParty()
		SmallWait(1)
		GiveItemCreate("JOMINHP1",Myself,0,0,0)
		SmallWait(1)
		EquipItemEx("JOMINHP1",0)
		SmallWait(1)
		ChangeEnemyAlly(Myself,FAMILIAR)
		AddFamiliar()
		FaceObject(Player1)
		SetGlobal("JO_JOIN_TALK","LOCALS",1) // fellow travelers
		SetGlobal("JO_JOIN","LOCALS",2)
		SetGlobalTimer("JO_JOIN_JOIN","LOCALS",ONE_TURN)
		ChangeAIScript("JO_JOINI",GENERAL)
// JO_JOIN.BAF		ChangeAIScript("%JO_JOIN%",OVERRIDE) // Go to relevant %JO_JOIN%.baf
		MakeGlobal()
		SetInterrupt(TRUE)
END

// fellow travelers (End first time from party to familiar)
 
IF
	Allegiance(Myself,FAMILIAR)
	Global("JO_JOIN_TALK","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_TALK","LOCALS",0)
		SetGlobal("JO_TRAVELER_%Death_var%","GLOBAL",1)
		DisplayStringHead(Myself,~We are now fellow travelers.~)
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// Switch between familiar and party

IF
	Global("JO_JOINI","LOCALS",1)
	Allegiance(Myself,PC)
	!InPartySlot(Myself,0)
	Delay(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		LeaveParty()
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		SmallWait(1)
		GiveItemCreate("JOMINHP1",Myself,0,0,0)
		SmallWait(1)
		EquipItemEx("JOMINHP1",0)
		SmallWait(1)
		ChangeEnemyAlly(Myself,FAMILIAR)
		AddFamiliar()
		SmallWait(1)
		SetGlobal("JO_JOINI","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",2)
		SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS) // 20mn
// Add switch familiar related  script
//		ChangeAIScript("",RACE)
//		ChangeAIScript("",CLASS)
//		ChangeAIScript("",GENERAL)
		ChangeAIScript("JO_JOINI",GENERAL)
		MakeGlobal()
// JO_JOIN.BAF		ChangeAIScript("%JO_JOIN%",OVERRIDE) // Go to relevant %JO_JOIN%.baf
		SetInterrupt(TRUE)
		Continue()
END
