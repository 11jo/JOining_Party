
///////////////////////////////////////////////////////////////////////////////////
// Loading travelers
// All travelers will switch in party

IF // reactive JOMINHP1 after load
	OnCreation()
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	HasItem("JOMINHP1",Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_SWITCHING_PARTY","GLOBAL",1) // Prevent Switching party blocks
		EquipItemEx("JOMINHP1",0)
		SetGlobalTimer("JO_JOIN_BANTER_TIME","LOCALS",ONE_DAY) //
		SetGlobal("JO_NEVER_JOIN","LOCALS",0) // Reset at loading
		SetGlobal("JO_NOJOIN","GLOBAL",0) // Reset at loading
		SetGlobal("JO_JOIN","LOCALS",0) // Reset at loading
		SetGlobal("JO_JOIN_LOAD","LOCALS",1)
		Continue()
END

///////////////////////////////////////////////////////////////////////////////////

IF
	!Global("JO_JOIN_HIDE_DURING_CUTSCENE","LOCALS",0)
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",1)
	IsActive(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_IS_HIDDEN_BY_CUTSCENE","LOCALS",1)
		Deactivate(Myself)
END

IF
	!Global("JO_JOIN_IS_HIDDEN_BY_CUTSCENE","LOCALS",0)
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	!IsActive(Myself)
THEN
	RESPONSE #100
		Activate(Myself)
		SetGlobal("JO_JOIN_IS_HIDDEN_BY_CUTSCENE","LOCALS",0)
		Continue()
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

IF
	Global("JO_JOIN_Move_Around","GLOBAL",0)
	Global("JO_JOIN_Move_Done","LOCALS",1)
THEN
	RESPONSE #1
		SetGlobal("JO_JOIN_Move_Done","LOCALS",0)
		Continue()
END

// Related to JOJOINNE.D groupe changes
//// No need to change JO_JOIN_CUTSCENE variable, next switch or next dialog or next cutscene will simply do it.
//// No need to change JO_NEVER_JOIN variable // Reset when loading a save.
IF
	!Global("JO_JOIN_Move_Around","GLOBAL",0)
	!Global("JO_JOIN_Move_Done","LOCALS",1)
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Switch("JO_JOIN_Move_Around","GLOBAL")
THEN
	RESPONSE #1
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_HIDE_DURING_CUTSCENE","LOCALS",1)
		Continue()
	RESPONSE #2
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_HIDE_DURING_CUTSCENE","LOCALS",0)
		Continue()
	RESPONSE #3
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",1)
		Continue()
	RESPONSE #4
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		Continue()
	RESPONSE #5
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_Myself_LeaveParty","LOCALS",1)
		Continue()
	RESPONSE #6
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		DisplayStringHead(Myself,~Youhou !~)
		Continue()
	RESPONSE #7
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_LOAD","LOCALS",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
		Continue()
	RESPONSE #8
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_LINK_TYPE","LOCALS",0)
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
		SetGlobal("BDAI_DISABLE_ATTACK","LOCALS",0)
		SetGlobal("BDAI_ATTACK_MODE","LOCALS",0)
		SetGlobal("BDAI_DISABLE_ITEMS","LOCALS",0)
		SetGlobal("BDAI_DISABLE_OFFENSIVE","LOCALS",0)
		SetGlobal("BDAI_DISABLE_DEFENSIVE","LOCALS",0)
		SetGlobal("BDAI_DISABLE_SPECIAL","LOCALS",0)
		SetGlobal("BDAI_SKILL_MODE","LOCALS",0)
		Continue()
	RESPONSE #9
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		MoveToObject(Player1)
		Continue()
	RESPONSE #10
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_NOSWITCH","LOCALS",1)
		Continue()
	RESPONSE #11
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_NOSWITCH","LOCALS",0)
		Continue()
	RESPONSE #12
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		SetGlobal("JO_JOIN_NOBANTER","LOCALS",0)
		SetGlobal("JO_JOIN_NOSWITCH","LOCALS",0)
		Continue()
	RESPONSE #13
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_NOBANTER","LOCALS",1)
		Continue()
	RESPONSE #14
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		SetGlobal("JO_JOIN_NOBANTER","LOCALS",0)
		Continue()
/*	RESPONSE #15
		SetGlobal("JO_JOIN_Move_Done","LOCALS",1)
		Continue()*/
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// Go to Reintegrate NPC into party periodically

IF // security check
	OR(2)
		OnCreation()
		Global("JO_JOIN_LOAD","LOCALS",1) // TODO: Added // JO_JOIN_LOAD allow to secure OnCreation() and next block
	!Global("JO_JOIN_IS_JOINING","LOCALS",0)
THEN
	RESPONSE #100
//		SetGlobal("JO_JOIN_JOIN","LOCALS",0) // TODO: // What ???
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
		Continue()
END

IF
	OR(2)
		Global("JO_JOIN_LOAD","LOCALS",1) // TODO: Added // JO_JOIN_LOAD allow to secure this block and previous block OnCreation()
		!GlobalTimerNotExpired("JO_JOIN_JOIN","LOCALS")
	ActionListEmpty()
	!ActuallyInCombat()
	Allegiance(Myself,FAMILIAR)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	Global("JO_NOJOIN","GLOBAL",0)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1) // != static mode
	NumInPartyLT(6)
	Switch("JO_JOIN_LINK_PLAYER_ID","LOCALS")
THEN
	RESPONSE #0
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player1Fill) // TODO: Added
		SetInterrupt(TRUE)
	RESPONSE #1
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player1) // TODO: Added
		SetInterrupt(TRUE)
	RESPONSE #2
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player2) // TODO: Added
		SetInterrupt(TRUE)
	RESPONSE #3
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player3) // TODO: Added
		SetInterrupt(TRUE)
	RESPONSE #4
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player4) // TODO: Added
		SetInterrupt(TRUE)
	RESPONSE #5
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player5) // TODO: Added
		SetInterrupt(TRUE)
	RESPONSE #6
		SetGlobal("JO_NOJOIN","GLOBAL",1)
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",1)
		SetInterrupt(FALSE)
		SmallWait(1)
		FaceObject(Player6) // TODO: Added
		SetInterrupt(TRUE)
END


///////////////////////////////////////////////////////////////////////////////

// Reintegrate NPC into party periodically (Every NINE_ROUNDS depending of other NPCs and Triggers above) 

// Could be changed to more or less rounds for players convenience

IF
	ActionListEmpty()
	!ActuallyInCombat()
	Allegiance(Myself,FAMILIAR)
	!Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	NumInPartyLT(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS) // 20mn
		SetGlobal("JO_JOIN_IS_JOINING","LOCALS",0)
		SetGlobal("JO_JOINI","LOCALS",1)
		SetGlobal("JO_JOIN_CLEAR","LOCALS",1) // ScriptAction (All_in_Baf)
		SetGlobal("JO_JOIN_LOAD","LOCALS",2)
//		EquipItemEx("JOMINHP1",1) // TODO: Added // Not needed for a quick switch // See related Switch between familiar and party in All_in
//		SmallWait(1)
//		DestroyItem("JOMINHP1")
		SmallWait(1)
		DisplayStringHead(Myself,~Hey.~)
		JoinParty() // Go to All_In_Baf Switch between familiar and party
		SetInterrupt(TRUE)
END

// Maybe something could be done with :

/*
0x0004 Joins(O:Object*)
Returns true if the specified object has joined the party in the last script round. This trigger is only sent to player characters. 

0x0005 Leaves(O:Object*)
Returns true if the specified object has left the party in the last script round. This trigger is only sent to player characters. 
*/

///////////////////////////////////////////////////////////////////////////////

// Broken links

IF // unexpected values
	OR(2)
		GlobalLT("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
		GlobalGT("JO_JOIN_LINK_PLAYER_ID","LOCALS",6)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END

IF
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(2)
		!InParty(Player1)
		!Exists(Player1) // TODO: useful ?
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END

IF
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",2)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(2)
		!InParty(Player2)
		!Exists(Player2)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END

IF
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",3)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(2)
		!InParty(Player3)
		!Exists(Player3)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END

IF
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",4)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(2)
		!InParty(Player4)
		!Exists(Player4)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END

IF
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",5)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(2)
		!InParty(Player5)
		!Exists(Player5)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END

IF
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",6)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(2)
		!InParty(Player6)
		!Exists(Player6)
THEN
	RESPONSE #100
		SetGlobal("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////////////
// Move to linked player

// Familiar without link: the linked player is the first player in the party (which may not be Player1)

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added // Prevent linked traveler to avoid a fight to follow a party member
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",0) // no linked
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1) // != static mode
	Global("JO_JOIN_CUTSCENE","GLOBAL",0) // no cutscene
	Allegiance(Myself,FAMILIAR)
	!Range(Player1Fill,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player1Fill) // JumpToObject(PlayerX) // MoveGlobalObject(Myself,PlayerX)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",0)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0) // follow with authorised move
	Global("JO_JOIN_CUTSCENE","GLOBAL",0) // no cutscene
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player1Fill,5) // PersonalSpaceDistance is more reliable than Range at such a low range
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2) // follow close
		!Range(Player1Fill,50)
THEN
	RESPONSE #100
		MoveToObject(Player1Fill)
END


// Linked to Player1

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",1)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!Range(Player1,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player1)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",1)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player1,5)
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2)
		!Range(Player1,50)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END


// Linked to Player2

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",2)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!Range(Player2,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player2)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",2)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player2,5)
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2)
		!Range(Player2,50)
THEN
	RESPONSE #100
		MoveToObject(Player2)
END


// Linked to Player3

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",3)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!Range(Player3,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player3)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",3)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player3,5)
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2)
		!Range(Player3,50)
THEN
	RESPONSE #100
		MoveToObject(Player3)
END


// Linked to Player4

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",4)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!Range(Player4,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player4)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",4)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player4,5)
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2)
		!Range(Player4,50)
THEN
	RESPONSE #100
		MoveToObject(Player4)
END


// Linked to Player5

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",5)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!Range(Player5,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player5)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",5)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player5,5)
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2)
		!Range(Player5,50)
THEN
	RESPONSE #100
		MoveToObject(Player5)
END


// Linked to Player6

// Large range or different area
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",6)
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!Range(Player6,70)
THEN
	RESPONSE #100
		MoveGlobalObject(Myself,Player6)
END

// Short range
IF
	ActionListEmpty()
	!ActuallyInCombat() // TODO: Added
	Global("JO_JOIN_LINK_PLAYER_ID","LOCALS",6)
	GlobalGT("JO_JOIN_LINK_TYPE","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	!PersonalSpaceDistance(Player6,5)
	OR(2)
		Global("JO_JOIN_LINK_TYPE","LOCALS",2)
		!Range(Player6,50)
THEN
	RESPONSE #100
		MoveToObject(Player6)
END


///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////


// Dead but not really  (// Related to AllInBaf set statut when Dead but not really )

IF
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	HPLT(Myself,2)
THEN
	RESPONSE #10
		DisplayStringHead(Myself,~Carry on without me...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Les carottes sont cuites...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~You're next...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Keep going, I will catch up...~)
		Continue()
	RESPONSE #10
		VerbalConstant(Myself,DYING)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~I can't fall... Not now !~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~I don't want to go...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~At last...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Huh... I can see the darkness !~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Go on without me, my fellow traveler~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Not like this... Not like this !~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Ooooh ! I can see the light !~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~<CHARNAME> ! For the gods sake...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Aaarghhh...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~The pointy ears had it right, we're all doomed.~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~My wounds are too grave. I am dead...~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~<CHARNAME> ! I curse you !!!~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~A fitting death !~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~We're all doomed! Run while you're still able.~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~I saw it coming... I guess.~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~I will return to finish you off.~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~Don't you dare leaving me here to rot !~)
		Continue()
	RESPONSE #10
		DisplayStringHead(Myself,~I know your true nature, <CHARNAME>. You're gonna kill all of us...~)
		Continue()
END

// Warning: conditions must be identical to the previous block
IF
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Allegiance(Myself,FAMILIAR)
	HPLT(Myself,2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SmallWait(1)
		SetGlobalTimer("JO_JOIN_TIMER_DEAD","LOCALS",ONE_TURN)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",1)
		ChangeEnemyAlly(Myself,NEUTRAL)
		SetSequence(SEQ_DIE)
		Wait(2)
		ReallyForceSpellRES("JOIN648",Myself)
		SetInterrupt(TRUE)
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// No more Dead  (// Related to AllInBaf set statut when No more Dead )

IF
	!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	OR(2)
		!ActuallyInCombat()
		!Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(3)
		!GlobalTimerNotExpired("JO_JOIN_TIMER_DEAD","LOCALS")
		HPGT(Myself,4)
		!Global("JO_JOIN_CUTSCENE","GLOBAL",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("JO_JOIN_JOIN","LOCALS",ONE_ROUND) // Temporaire por les tests ou utile pour les joueurs ?
		ChangeEnemyAlly(Myself,FAMILIAR)
		ReallyForceSpellRES("JOIR648",Myself)
		Wait(2)
		SetSequence(SEQ_EMERGE)
		SetInterrupt(TRUE)
		Continue() // Important
END

// Warning: conditions must be identical to the previous block
IF
	!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	OR(2)
		!ActuallyInCombat()
		!Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	OR(3)
		!GlobalTimerNotExpired("JO_JOIN_TIMER_DEAD","LOCALS")
		HPGT(Myself,4)
		!Global("JO_JOIN_CUTSCENE","GLOBAL",0)
THEN
	RESPONSE #10
		DisplayStringHead(Myself,~Still alive !~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~Dead ? No more !~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~The sun shines, and I am amazed to see another day.~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~Perhaps I'll survive longer than I had originally thought.~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~You didn't abandon me !~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~It was a... Nice experiment...~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~Well, I guess we have to go on...~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~I may have overreacted.~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~Of "curse"... I didn't mean it.~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	RESPONSE #10
		DisplayStringHead(Myself,~Well, that escalated quickly...~)
		SetGlobal("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// NOT TODO: les états visuellement identifiables, les états qui ne sont pas compatibles avec les scripts
// un display reste une information peu explicite dans un combat (trop de log), comment améliorer ça ?

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	StateCheck(Myself,STATE_POISONED)
	CheckStatLT(Myself,100,RESISTPOISON)
	Delay(12)
THEN
	RESPONSE #40
		DisplayString(Myself,~Damn venom !~)
		Continue()
	RESPONSE #30
		DisplayString(Myself,~My blood is on fire.~)
		Continue()
	RESPONSE #30
		DisplayString(Myself,~I am toxic.~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	CheckStatLT(Myself,4,VISUALRANGE) // plus général que STATE_BLIND
	Delay(60)
THEN
	RESPONSE #50
		DisplayString(Myself,~I can't see...~)
		Continue()
	RESPONSE #50
		DisplayString(Myself,~Who turned off the light ?~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	CheckSpellState(Myself,DISEASED)
	Delay(60)
THEN
	RESPONSE #50
		DisplayString(Myself,~I am infected !~)
		Continue()
	RESPONSE #50
		DisplayString(Myself,~It's probably contagious...~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	CheckSpellState(Myself,DEAFENED)
	Delay(60)
THEN
	RESPONSE #40
		DisplayString(Myself,~Speak up, speak up !~) // Parlez plus fort, plus fort ! (@11086)
		Continue()
	RESPONSE #30
		DisplayString(Myself,~What did you say ?~)
		Continue()
	RESPONSE #30
		DisplayString(Myself,~I can't hear you...~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	CheckStatGT(Myself,49,SPELLFAILUREMAGE)
	!CheckSpellState(Myself,DEAFENED) // on exclu le cas de la surdité (géré à part)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE) // on exclu le cas des insectes (visible)
	OR(2)
		Class(Myself,MAGE_ALL)
		Class(Myself,BARD_ALL)
	Delay(30)
THEN
	RESPONSE #50
		DisplayString(Myself,~Concentration is difficult.~)
		Continue()
	RESPONSE #50
		DisplayString(Myself,~It is better not to cast spells.~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	CheckStatGT(Myself,49,SPELLFAILUREPRIEST)
	!CheckSpellState(Myself,DEAFENED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	OR(4)
		Class(Myself,CLERIC_ALL)
		Class(Myself,PALADIN_ALL)
		Class(Myself,DRUID_ALL)
		Class(Myself,RANGER_ALL)
	Delay(30)
THEN
	RESPONSE #50
		DisplayString(Myself,~Concentration is difficult.~)
		Continue()
	RESPONSE #50
		DisplayString(Myself,~It is better not to cast spells.~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	StateCheck(Myself,STATE_SILENCED)
	Delay(60)
THEN
	RESPONSE #50
		DisplayString(Myself,~<GABBER> wave at you.~)
		Continue()
	RESPONSE #50
		DisplayString(Myself,~<GABBER> open they lips but not sound comes out...~)
		Continue()
END

IF
	ActionListEmpty()
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	Allegiance(Myself,FAMILIAR)
	OR(2)
		CheckStatGT(Myself,6,FATIGUE)
		CheckSpellState(Myself,BERSERKER_RAGE_FATIGUE)
	Delay(240)
THEN
	RESPONSE #100
		VerbalConstant(Myself,TIRED)
		Continue()
END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Use Potions

IF
	ActionListEmpty()
	Global("BDAI_DISABLE_ITEMS","LOCALS",0)
	!GlobalTimerNotExpired("BD_Cast","LOCALS")
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	CheckStatLT(Myself,100,RESISTPOISON)
	OR(2)
		StateCheck(Myself,STATE_POISONED)
		CheckSpellState(Myself,DISEASED)
	HasItemEquiped("POTN17",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("BD_Cast","LOCALS",ONE_ROUND)
		UseItem("POTN17",Myself)
END

IF
	ActionListEmpty()
	Global("BDAI_DISABLE_ITEMS","LOCALS",0)
	!GlobalTimerNotExpired("BD_Cast","LOCALS")
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	CheckStatLT(Myself,100,RESISTPOISON)
	StateCheck(Myself,STATE_POISONED)
	HasItemEquiped("POTN20",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("BD_Cast","LOCALS",ONE_ROUND)
		UseItem("POTN20",Myself)
END

IF
	ActionListEmpty()
	ActuallyInCombat()
	Global("BDAI_DISABLE_ITEMS","LOCALS",0)
	!GlobalTimerNotExpired("BD_Cast","LOCALS")
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	HPPercentLT(Myself,60)
	OR(2)
		CheckStatGT(Myself,99,MAXHITPOINTS)
		HPPercentLT(Myself,40)
	HasItemEquiped("POTN55",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("BD_Cast","LOCALS",ONE_ROUND)
		UseItem("POTN55",Myself)
END

IF
	ActionListEmpty()
	ActuallyInCombat()
	Global("BDAI_DISABLE_ITEMS","LOCALS",0)
	!GlobalTimerNotExpired("BD_Cast","LOCALS")
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	HPPercentLT(Myself,60)
	OR(2)
		CheckStatGT(Myself,67,MAXHITPOINTS)
		HPPercentLT(Myself,40)
	HasItemEquiped("POTN52",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("BD_Cast","LOCALS",ONE_ROUND)
		UseItem("POTN52",Myself)
END

IF
	ActionListEmpty()
	ActuallyInCombat()
	Global("BDAI_DISABLE_ITEMS","LOCALS",0)
	!GlobalTimerNotExpired("BD_Cast","LOCALS")
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	HPPercentLT(Myself,60)
	OR(2)
		CheckStatGT(Myself,22,MAXHITPOINTS)
		HPPercentLT(Myself,40)
	HasItemEquiped("POTN08",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("BD_Cast","LOCALS",ONE_ROUND)
		UseItem("POTN08",Myself)
END

//

// Basic switch

IF
	ActionListEmpty()
	IsWeaponRanged(Myself)
	OR(3)
		Global("BDAI_ATTACK_MODE","LOCALS",1) // melee
		!Global("BDAI_DISABLE_ATTACK","LOCALS",0) // no attack
		PersonalSpaceDistance(NearestEnemyOf(Myself),4)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
END

IF
	ActionListEmpty()
	Global("BDAI_ATTACK_MODE","LOCALS",2) // ranged
	!IsWeaponRanged(Myself)
	CanEquipRanged()
	!PersonalSpaceDistance(NearestEnemyOf(Myself),4)
THEN
	RESPONSE #100
		EquipRanged()
END

IF
	ActionListEmpty()
	Global("BDAI_ATTACK_MODE","LOCALS",2) // ranged
	Global("BDAI_DISABLE_ATTACK","LOCALS",0) // can attack
	!CanEquipRanged()
	Delay(30)
THEN
	RESPONSE #100
		DisplayString(Myself,~I cannot use my ranged weapon.~)
END

//

// Modales

IF
	Global("BDAI_SKILL_MODE","LOCALS",1) // find traps
	ActionListEmpty()
	ModalState(NONE)
	!ButtonDisabled(SCREEN_INVENTORY)
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	OR(2)
		CheckStatGT(Myself,0,TRAPS)
		CheckStatGT(Myself,0,DETECTILLUSIONS)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		FindTraps()
END

IF
	Global("BDAI_SKILL_MODE","LOCALS",2) // hide
	ActionListEmpty()
	ModalState(NONE)
	!ButtonDisabled(BUTTON_STEALTH)
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	CheckStat(Myself,0,SANCTUARY)
	!StateCheck(Myself,STATE_INVISIBLE)
	OR(2)
		CheckStatGT(Myself,0,HIDEINSHADOWS)
		CheckStatGT(Myself,0,STEALTH)
	OR(2)
		!See(NearestEnemyOf(Myself))
		Kit(Myself,SHADOWDANCER)
THEN
	RESPONSE #100
		Hide()
END

IF
	Global("BDAI_SKILL_MODE","LOCALS",3) // danse
	ActionListEmpty()
	ModalState(NONE)
	Class(Myself,SHAMAN)
	!ButtonDisabled(BUTTON_BATTLESONG)
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
THEN
	RESPONSE #100
		BattleSong()
END

IF
	Global("BDAI_SKILL_MODE","LOCALS",3) // song
	ActionListEmpty()
	ModalState(NONE)
	Class(Myself,BARD)
	!ButtonDisabled(BUTTON_BATTLESONG)
	!StateCheck(Myself,STATE_DISABLED_CASTING)
	CheckStat(Myself,0,CASTERHOLD)
	Allegiance(Myself,GOODCUTOFF)
THEN
	RESPONSE #100
		BattleSong()
END

IF
	Global("BDAI_SKILL_MODE","LOCALS",4) // repulse undead
	ActionListEmpty()
	ModalState(NONE)
	!ButtonDisabled(BUTTON_TURNUNDEAD)
	!StateCheck(Myself,STATE_DISABLED)
	CheckStat(Myself,0,CASTERHOLD)
	!CheckStat(Myself,0,TURNUNDEADLEVEL)
	See(NearestEnemyOfType([0.UNDEAD]))
THEN
	RESPONSE #100
		Turn()
END

//

// 

IF
	ActionListEmpty()
	ActuallyInCombat()
	!Global("JO_JOIN_LINK_TYPE","LOCALS",-1) // can move
	HPPercentLT(Myself,20)
	See(LastAttackerOf(Myself))
	Range(LastSeenBy(Myself),8)
THEN
	RESPONSE #100
		RunAwayFromNoLeaveArea(LastSeenBy(Myself),45)
END

// Basic attack

IF
	ActionListEmpty()
	Global("BDAI_DISABLE_ATTACK","LOCALS",0)
	See(NearestEnemyOf(Myself))
	OR(2)
		!Global("JO_JOIN_LINK_TYPE","LOCALS",-1) // can move
		InWeaponRange(LastSeenBy(Myself))
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

///////////////////////////////////////////////////////////////////////////////
// Switching party !!!

// Related to JO_JOIN_Wannabe in All_in

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	Global("JO_JOIN_LET_BANTER","LOCALS",0)
//	Global("JO_JOIN_IS_SWITCHING","LOCALS",0)
	Global("JO_JOIN_READY_TO_SWITCH","LOCALS",1)
	ActionListEmpty()
	!ActuallyInCombat()
	Global("JO_Myself_Valid","LOCALS",1)
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_NOJOIN","GLOBAL",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	NumInPartyLT(6)
THEN
	RESPONSE #0
//		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",1)
		SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",2)
		SetGlobal("JO_NOJOIN","GLOBAL",1)
END
/*
////////////////////////// ON HOLD
// Some empty slots in party, Just JoinParty() !

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	ActionListEmpty()
	!ActuallyInCombat()
	Global("JO_JOIN_IS_SWITCHING","LOCALS",1)
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	NumInPartyLT(4)
THEN
	RESPONSE #25
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,~Let me in !~)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SmallWait(2)
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #25
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,~Incoming !~)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SmallWait(2)
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #25
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,~There is plenty of room in this group...~)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SmallWait(2)
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #25
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,~I feel you went ahead too early...~)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SmallWait(2)
		JoinParty()
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		SetInterrupt(TRUE)
END
////////////////////////// ON HOLD
*/

// No free slots in party, then randomly select a member... Wannabe !

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	RandomNum(5,1)
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	TriggerOverride(Player2,Global("JO_JOIN_READY_TO_SWITCH","LOCALS",1))
	Global("JO_JOIN_LET_BANTER","LOCALS",0)
//	Global("JO_JOIN_IS_SWITCHING","LOCALS",1)
	Global("JO_JOIN_READY_TO_SWITCH","LOCALS",2)
	ActionListEmpty()
	!ActuallyInCombat()
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	TriggerOverride(Player2,Global("JO_Myself_Valid","LOCALS",1))
	TriggerOverride(Player2,Global("JO_JOIN_Wannabe","LOCALS",1))
	NumInPartyGT(4)
	NumInPartyLT(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		FaceObject(Player2)
//		DisplayStringHead(Myself,~Hey <PLAYER2> !~)
		ActionOverride(Player2,SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0))
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",2)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	RandomNum(5,2)
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	TriggerOverride(Player3,Global("JO_JOIN_READY_TO_SWITCH","LOCALS",1))
	Global("JO_JOIN_LET_BANTER","LOCALS",0)
//	Global("JO_JOIN_IS_SWITCHING","LOCALS",1)
	Global("JO_JOIN_READY_TO_SWITCH","LOCALS",2)
	ActionListEmpty()
	!ActuallyInCombat()
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	TriggerOverride(Player3,Global("JO_Myself_Valid","LOCALS",1))
	TriggerOverride(Player3,Global("JO_JOIN_Wannabe","LOCALS",1))
	NumInPartyGT(4)
	NumInPartyLT(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		FaceObject(Player3)
//		DisplayStringHead(Myself,~Hey <PLAYER3> !~)
		ActionOverride(Player3,SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0))
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",3)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	RandomNum(5,3)
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	TriggerOverride(Player4,Global("JO_JOIN_READY_TO_SWITCH","LOCALS",1))
	Global("JO_JOIN_LET_BANTER","LOCALS",0)
//	Global("JO_JOIN_IS_SWITCHING","LOCALS",1)
	Global("JO_JOIN_READY_TO_SWITCH","LOCALS",2)
	ActionListEmpty()
	!ActuallyInCombat()
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	TriggerOverride(Player4,Global("JO_Myself_Valid","LOCALS",1))
	TriggerOverride(Player4,Global("JO_JOIN_Wannabe","LOCALS",1))
	NumInPartyGT(4)
	NumInPartyLT(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		FaceObject(Player4)
//		DisplayStringHead(Myself,~Hey <PLAYER4> !~)
		ActionOverride(Player4,SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0))
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",4)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	RandomNum(5,4)
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	TriggerOverride(Player5,Global("JO_JOIN_READY_TO_SWITCH","LOCALS",1))
	Global("JO_JOIN_LET_BANTER","LOCALS",0)
//	Global("JO_JOIN_IS_SWITCHING","LOCALS",1)
	Global("JO_JOIN_READY_TO_SWITCH","LOCALS",2)
	ActionListEmpty()
	!ActuallyInCombat()
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	TriggerOverride(Player5,Global("JO_Myself_Valid","LOCALS",1))
	TriggerOverride(Player5,Global("JO_JOIN_Wannabe","LOCALS",1))
	NumInPartyGT(4)
	NumInPartyLT(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		FaceObject(Player5)
//		DisplayStringHead(Myself,~Hey <PLAYER5> !~)
		ActionOverride(Player5,SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0))
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",5)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	RandomNum(5,5)
	!GlobalTimerNotExpired("JO_JOIN_SWITCH_TIMER","LOCALS")
	TriggerOverride(Player6,Global("JO_JOIN_READY_TO_SWITCH","LOCALS",1))
	Global("JO_JOIN_LET_BANTER","LOCALS",0)
//	Global("JO_JOIN_IS_SWITCHING","LOCALS",1)
	Global("JO_JOIN_READY_TO_SWITCH","LOCALS",2)
	ActionListEmpty()
	!ActuallyInCombat()
	OR(2)
		Allegiance(Myself,FAMILIAR)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",1)
	Global("JO_JOIN_IS_JOINING","LOCALS",0)
	Global("JO_JOIN_CUTSCENE","GLOBAL",0)
	Global("JO_JOIN","LOCALS",0)
	Global("JO_JOINI","LOCALS",0)
	Global("JO_NEVER_JOIN","LOCALS",0)
	!Global("JO_JOIN_LOAD","LOCALS",1)
	TriggerOverride(Player6,Global("JO_Myself_Valid","LOCALS",1))
	TriggerOverride(Player6,Global("JO_JOIN_Wannabe","LOCALS",1))
	NumInPartyGT(4)
	NumInPartyLT(6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		FaceObject(Player6)
//		DisplayStringHead(Myself,~Hey <PLAYER6> !~)
		ActionOverride(Player6,SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0))
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",6)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("JO_JOIN_SWITCHING_PARTY","GLOBAL",0) // Used by loading blocks
	NumInPartyLT(6)
	Switch("JO_JOIN_LET_BANTER","LOCALS")
THEN
	RESPONSE #2
		SetInterrupt(FALSE)
		ActionOverride(Player2,FaceObject(Myself))
		DisplayStringHead(Player2,~Hé ?~)
		ActionOverride(Player2,SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS))
		ActionOverride(Player2,SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS))
		SmallWait(1)
		DisplayStringHead(Myself,~My turn !~)
		ActionOverride(Player2,SetGlobal("JO_JOIN","LOCALS",1))
		Wait(1)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",0)
		SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0)
//		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SmallWait(1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SmallWait(2)
		EquipItemEx("JOMINHP1",1)
		SmallWait(1)
		DestroyItem("JOMINHP1")
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #3
		SetInterrupt(FALSE)
		ActionOverride(Player3,FaceObject(Myself))
		DisplayStringHead(Player3,~Yes ?~)
		ActionOverride(Player3,SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS))
		ActionOverride(Player3,SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS))
		SmallWait(1)
		DisplayStringHead(Myself,~My turn !~)
		ActionOverride(Player3,SetGlobal("JO_JOIN","LOCALS",1))
		Wait(1)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",0)
		SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0)
//		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SmallWait(1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SmallWait(2)
		EquipItemEx("JOMINHP1",1)
		SmallWait(1)
		DestroyItem("JOMINHP1")
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #4
		SetInterrupt(FALSE)
		ActionOverride(Player4,FaceObject(Myself))
		DisplayStringHead(Player4,~Don't disturb...~)
		ActionOverride(Player4,SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS))
		ActionOverride(Player4,SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS))
		SmallWait(1)
		DisplayStringHead(Myself,~My turn !~)
		ActionOverride(Player4,SetGlobal("JO_JOIN","LOCALS",1))
		Wait(1)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",0)
		SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0)
//		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SmallWait(1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SmallWait(2)
		EquipItemEx("JOMINHP1",1)
		SmallWait(1)
		DestroyItem("JOMINHP1")
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #5
		SetInterrupt(FALSE)
		ActionOverride(Player5,FaceObject(Myself))
		DisplayStringHead(Player5,~What ?~)
		ActionOverride(Player5,SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS))
		ActionOverride(Player5,SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS))
		SmallWait(1)
		DisplayStringHead(Myself,~My turn !~)
		ActionOverride(Player5,SetGlobal("JO_JOIN","LOCALS",1))
		Wait(1)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",0)
		SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0)
//		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SmallWait(1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SmallWait(2)
		EquipItemEx("JOMINHP1",1)
		SmallWait(1)
		DestroyItem("JOMINHP1")
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
	RESPONSE #6
		SetInterrupt(FALSE)
		ActionOverride(Player6,FaceObject(Myself))
		DisplayStringHead(Player6,~Hum ?~)
		ActionOverride(Player6,SetGlobalTimer("JO_JOIN_JOIN","LOCALS",FOUR_HOURS))
		ActionOverride(Player6,SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS))
		SmallWait(1)
		DisplayStringHead(Myself,~My turn !~)
		ActionOverride(Player6,SetGlobal("JO_JOIN","LOCALS",1))
		Wait(1)
		SetGlobalTimer("JO_JOIN_SWITCH_TIMER","LOCALS",TWELVE_HOURS)
		SetGlobal("JO_JOIN_LET_BANTER","LOCALS",0)
		SetGlobal("JO_JOIN_READY_TO_SWITCH","LOCALS",0)
//		SetGlobal("JO_JOIN_IS_SWITCHING","LOCALS",0)
		SmallWait(1)
		SetGlobal("JO_NEVER_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_IS_TRAVELER","LOCALS",0)
		SmallWait(2)
		EquipItemEx("JOMINHP1",1)
		SmallWait(1)
		DestroyItem("JOMINHP1")
		SetGlobal("JO_NOJOIN","GLOBAL",0)
		JoinParty()
		SetInterrupt(TRUE)
END
