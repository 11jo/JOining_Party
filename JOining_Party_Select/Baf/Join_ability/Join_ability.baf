// Extends DPLAYER3.bcs

IF
	Global("JO_JOIN_PLAYER_INIT","LOCALS",0)
THEN
	RESPONSE #100
		AddSpecialAbility("jo_join")
		SetGlobal("JO_JOIN_PLAYER_INIT","LOCALS",1)
		Continue()
END

///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////

// Base
 
// !InPartyAllowDead()
IF
	!Global("JO_Myself_AllowDead","LOCALS",0)
	!InPartyAllowDead(Myself)
	Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_AllowDead","LOCALS",0)
		Continue()
END
 
// InPartyAllowDead()
IF
	Global("JO_Myself_AllowDead","LOCALS",0)
	OR(2)
		InPartyAllowDead(Myself)
		!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_AllowDead","LOCALS",1)
		Continue()
END
 
// !InParty()
IF
	!Global("JO_Myself_InParty","LOCALS",0)
	!InParty(Myself)
	OR(2)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
		!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_InParty","LOCALS",0)
		Continue()
END
 
// InParty()
IF
	Global("JO_Myself_InParty","LOCALS",0)
	OR(2)
		InParty(Myself)
		!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
	OR(2)
		InParty(Myself)
		Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_InParty","LOCALS",1)
		Continue()
END
 
// !Is(!If)ValidForPartyDialog(ue)
IF
	!Global("JO_Myself_Valid","LOCALS",0)
	!IsValidForPartyDialogue(Myself)
	OR(4)
		Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
		!Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
		StateCheck(Myself,CD_STATE_NOTVALID)
		!IsActive(Myself)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_Valid","LOCALS",0)
		Continue()
END
 
// Is(If)ValidForPartyDialog(ue)
IF
	Global("JO_Myself_Valid","LOCALS",0)
	Global("JO_JOIN_SLEEPING_DEAD","LOCALS",0)
	IsActive(Myself)
	OR(2)
		IsValidForPartyDialogue(Myself)
		!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		InParty(Myself)
		!Global("JO_JOIN_IS_TRAVELER","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("JO_Myself_Valid","LOCALS",1)
		Continue()
END

// Special security / JOJOINNE
IF
	Global("JO_JOIN","LOCALS",1)
THEN
	RESPONSE #100
//		SetGlobal("JO_NOJOIN","GLOBAL",0)
		SetGlobal("JO_JOIN","LOCALS",0)
		SetGlobal("JO_JOIN_CUTSCENE","GLOBAL",0)
		CreateCreatureObject("JOJOINNE",Myself,0,0,0)
		Continue()
END

IF
	HasItem("JOMINHP1",Player1)
THEN
	RESPONSE #100
		DisplayStringHead(Myself, ~^RFIXME. Voilà qui n'est pas normal... JOMINHP1 est dans mon inventaire !^-~)
		EquipItemEx("JOMINHP1",1)
		DestroyItem("JOMINHP1")
END

IF
	GlobalLT("JO_JOIN_PARTY_SWITCH","GLOBAL",0)
THEN
	RESPONSE #100
		DisplayStringHead(Myself, ~^RFIXME. Voilà qui n'est pas normal... JO_JOIN_PARTY_SWITCH est inférieur à 0 !^-~)
		SetGlobal("JO_JOIN_PARTY_SWITCH","GLOBAL",0)
END

IF
	GlobalLT("JO_JOIN_TRAVELER_SWITCH","GLOBAL",0)
THEN
	RESPONSE #100
		DisplayStringHead(Myself, ~^RFIXME. Voilà qui n'est pas normal... JO_JOIN_TRAVELER_SWITCH est inférieur à 0 !^-~)
		SetGlobal("JO_JOIN_TRAVELER_SWITCH","GLOBAL",0)
END
